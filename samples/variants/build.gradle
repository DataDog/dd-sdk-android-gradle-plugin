apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.datadoghq.dd-sdk-android-gradle-plugin'

import com.datadog.gradle.config.AndroidConfig

android {
    compileSdkVersion = AndroidConfig.TARGET_SDK
    buildToolsVersion = AndroidConfig.BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.datadog.example.variants"
        minSdkVersion AndroidConfig.MIN_SDK
        targetSdkVersion AndroidConfig.TARGET_SDK
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.getByName("debug")
        }
    }

    namespace = "com.datadog.example.variants"

    sourceSets.named("main") {
        java.srcDir("src/main/kotlin")
    }

    flavorDimensions("version", "colour")
    productFlavors {
        demo {
            dimension "version"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }
        full {
            dimension "version"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
        pro {
            dimension "version"
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"
        }

        red {
            dimension "colour"
        }
        green {
            dimension "colour"
        }
        blue {
            dimension "colour"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation(project(':samples:lib-module'))
    implementation(libs.kotlin)

    implementation(libs.androidx.core)
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.constraintlayout)

    implementation(libs.datadogSdkRum)
    implementation(libs.datadogSdkLogs)
}

datadog {
    site = "STAGING"
}

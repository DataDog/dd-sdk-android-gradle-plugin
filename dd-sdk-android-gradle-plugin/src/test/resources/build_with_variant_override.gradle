plugins {
    id("com.android.application")
    id("kotlin-android")
    id("com.datadoghq.dd-sdk-android-gradle-plugin")
}

repositories {
    google()
    mavenCentral()
}

android {
    compileSdkVersion = rootProject.ext.targetSdkVersion
    buildToolsVersion = rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.variants"
        minSdkVersion 23
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true
    }

    namespace = "com.example.variants"

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    compileOptions {
        sourceCompatibility = rootProject.ext.jvmTarget
        targetCompatibility = rootProject.ext.jvmTarget
    }

    flavorDimensions("version", "colour")
    productFlavors {
        pro {
            dimension "version"
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"
        }

        green {
            dimension "colour"
        }
        blue {
            dimension "colour"
        }
    }
}

if (Integer.parseInt(rootProject.ext.agpVersion.split("\\.")[0]) >= 9) {
    kotlin {
        compilerOptions {
            jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.fromTarget(rootProject.ext.jvmTarget)
        }
    }
} else {
    android {
        kotlinOptions {
            jvmTarget = rootProject.ext.jvmTarget
        }
    }
}

dependencies {
    implementation(rootProject.ext.datadogSdkDependency)
}

datadog {
    remoteRepositoryUrl = "http://github.com:fakeapp/repository.git"

    variants {
        pro {
            remoteRepositoryUrl = "http://github.com:fakeapp-another/repository.git"
        }
    }
}
